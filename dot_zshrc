#!/bin/zsh
##
# ⚠️ WARNING: Don't manually `source` your .zshrc file! It can have unexpected
# side effects. Instead, to safely apply changes, use `znap restart`.
#
# Load Dotfile defaults
. "$HOME/.dotfiles/path_defaults.zsh"

ZNAP_ROOT="$HOME/.znap"
ZNAP_REPO="$ZNAP_ROOT/repo"
ZNAP_PLUGINS="$ZNAP_ROOT/plugins"
# Set the dir for znap itself
zstyle ':znap:*' repos-dir $ZNAP_REPO
# Set the plugins directory for znap (Why does this not work :( )
# zstyle ':znap:*' plugins-dir $ZNAP_PLUGINS

# Download Znap, if it's not there yet.
[[ -f $ZNAP_REPO/znap.zsh ]] ||
    git clone --depth 1 -- \
        https://github.com/marlonrichert/zsh-snap.git $ZNAP_REPO

source "$ZNAP_REPO/znap.zsh"  # Start Znap

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load plugins
. "$DOTFILES/znap/plugins.zsh"

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.localrc ] && . ~/.localrc
source "${XDG_CONFIG_HOME:-$HOME/.config}/asdf-direnv/zshrc"
